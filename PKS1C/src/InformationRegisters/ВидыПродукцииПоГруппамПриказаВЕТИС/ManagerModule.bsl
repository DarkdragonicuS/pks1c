
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции
Функция ВерсияПеречняСодержитГруппу35()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВидыПродукцииПоГруппамПриказаВЕТИС.ВидПродукцииИдентификатор КАК ВидПродукцииИдентификатор
	|ИЗ
	|	РегистрСведений.ВидыПродукцииПоГруппамПриказаВЕТИС КАК ВидыПродукцииПоГруппамПриказаВЕТИС
	|ГДЕ
	|	ВидыПродукцииПоГруппамПриказаВЕТИС.ГруппаПриказа = Значение(Перечисление.ГруппыПродукцииУполномоченныхЛиц.Группа35Строка1)";
	
	Возврат Не Запрос.Выполнить().Пустой()
	
КонецФункции // ВерсияПеречняСодержитГруппу35()

Функция ДанныеОбновленыНаНовуюВерсиюПрограммы(МетаданныеИОтбор) Экспорт

	Возврат ВерсияПеречняСодержитГруппу35();

КонецФункции // ДанныеОбновленыНаНовуюВерсиюПрограммы()

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра",             "РегистрСведений.ВидыПродукцииПоГруппамПриказаВЕТИС");
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ЭтоДвижения",                   Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыПродукцииПоГруппамПриказаВЕТИС.РольПользователя КАК РольПользователя,
	|	ВидыПродукцииПоГруппамПриказаВЕТИС.ГруппаПриказа КАК ГруппаПриказа,
	|	ВидыПродукцииПоГруппамПриказаВЕТИС.ВидПродукцииИдентификатор КАК ВидПродукцииИдентификатор
	|ИЗ
	|	РегистрСведений.ВидыПродукцииПоГруппамПриказаВЕТИС КАК ВидыПродукцииПоГруппамПриказаВЕТИС";
	
	Данные = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	УзелРегистрации = ПланыОбмена.ОбновлениеИнформационнойБазы.УзелПоОчереди(Параметры.Очередь);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПодконтрольныхТоваровВЕТИС")
		Или ВерсияПеречняСодержитГруппу35() Тогда
		
		Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПодконтрольныхТоваровВЕТИС") Тогда
			НаборЗаписей = РегистрыСведений.ВидыПродукцииПоГруппамПриказаВЕТИС.СоздатьНаборЗаписей();
			НаборЗаписей.Записать();
		КонецЕсли;
		
		ПланыОбмена.УдалитьРегистрациюИзменений(УзелРегистрации, Метаданные.РегистрыСведений.ВидыПродукцииПоГруппамПриказаВЕТИС);
		Параметры.ОбработкаЗавершена = Истина;
		
		Возврат;
		
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ВидыПродукцииПоГруппамПриказаВЕТИС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ВидыПродукцииПоГруппамПриказаВЕТИС.СоздатьНаборЗаписей();
		НаборЗаписей.Записать();
		
		ДопустимыеЦелиВЕТИС.ЗаполнитьДанныеВРегистреВидыПродукцииПоГруппамПриказаВЕТИС();
		
		ПланыОбмена.УдалитьРегистрациюИзменений(УзелРегистрации, Метаданные.РегистрыСведений.ВидыПродукцииПоГруппамПриказаВЕТИС);
		
		Параметры.ОбработкаЗавершена = Истина;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось обработать регистр ВидыПродукцииПоГруппамПриказаВЕТИС по причине: %1'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.ВидыПродукцииПоГруппамПриказаВЕТИС,,
			ТекстСообщения);
		Параметры.ОбработкаЗавершена = Ложь;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли