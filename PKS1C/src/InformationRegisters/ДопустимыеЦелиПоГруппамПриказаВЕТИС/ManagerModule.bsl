
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции
Функция ВерсияПеречняСодержитГруппу35()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДопустимыеЦелиПоГруппамПриказаВЕТИС.ГруппаПриказа КАК ГруппаПриказа
	|ИЗ
	|	РегистрСведений.ДопустимыеЦелиПоГруппамПриказаВЕТИС КАК ДопустимыеЦелиПоГруппамПриказаВЕТИС
	|ГДЕ
	|	ДопустимыеЦелиПоГруппамПриказаВЕТИС.ГруппаПриказа = ЗНАЧЕНИЕ(Перечисление.ГруппыПродукцииУполномоченныхЛиц.Группа35Строка1)";
	
	Возврат Не Запрос.Выполнить().Пустой()
	
КонецФункции // ВерсияПеречняСодержитГруппу35()

Функция ДанныеОбновленыНаНовуюВерсиюПрограммы(МетаданныеИОтбор) Экспорт

	Возврат ВерсияПеречняСодержитГруппу35();

КонецФункции // ДанныеОбновленыНаНовуюВерсиюПрограммы()

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра",             "РегистрСведений.ДопустимыеЦелиПоГруппамПриказаВЕТИС");
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ЭтоДвижения",                   Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДопустимыеЦелиПоГруппамПриказаВЕТИС.ГруппаПриказа КАК ГруппаПриказа,
	|	ДопустимыеЦелиПоГруппамПриказаВЕТИС.ЦельИдентификатор КАК ЦельИдентификатор
	|ИЗ
	|	РегистрСведений.ДопустимыеЦелиПоГруппамПриказаВЕТИС КАК ДопустимыеЦелиПоГруппамПриказаВЕТИС";
	
	Данные = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	УзелРегистрации = ПланыОбмена.ОбновлениеИнформационнойБазы.УзелПоОчереди(Параметры.Очередь);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПодконтрольныхТоваровВЕТИС")
		Или ВерсияПеречняСодержитГруппу35() Тогда
		
		Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПодконтрольныхТоваровВЕТИС") Тогда
			НаборЗаписей = РегистрыСведений.ДопустимыеЦелиПоГруппамПриказаВЕТИС.СоздатьНаборЗаписей();
			НаборЗаписей.Записать();
		КонецЕсли;
		
		Параметры.ОбработкаЗавершена = Истина;
		ПланыОбмена.УдалитьРегистрациюИзменений(УзелРегистрации, Метаданные.РегистрыСведений.ДопустимыеЦелиПоГруппамПриказаВЕТИС);
		
		Возврат;
		
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ДопустимыеЦелиПоГруппамПриказаВЕТИС");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ДопустимыеЦелиПоГруппамПриказаВЕТИС.СоздатьНаборЗаписей();
		НаборЗаписей.Записать();
		
		ДопустимыеЦелиВЕТИС.ЗаполнитьДанныеВРегистреДопустимыеЦелиПоГруппамПриказаВЕТИС();
		
		ПланыОбмена.УдалитьРегистрациюИзменений(УзелРегистрации, Метаданные.РегистрыСведений.ДопустимыеЦелиПоГруппамПриказаВЕТИС);
		Параметры.ОбработкаЗавершена = Истина;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось обработать регистр ДопустимыеЦелиПоГруппамПриказаВЕТИС по причине: %1'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.ДопустимыеЦелиПоГруппамПриказаВЕТИС,,
			ТекстСообщения);
		Параметры.ОбработкаЗавершена = Ложь;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли